buildscript {
    ext {
        springBootVersion = '2.0.5.RELEASE'
    }
    repositories {
        mavenCentral()
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath "com.moowork.gradle:gradle-node-plugin:1.2.0"
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'com.moowork.node'

group = 'com.kotlarz'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url "https://repo.spring.io/snapshot" }
    maven { url "https://repo.spring.io/milestone" }
}

dependencies {
    compile('org.springframework.boot:spring-boot-starter-data-jpa')
    compile('org.springframework.boot:spring-boot-starter-security')
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.springframework.boot:spring-boot-starter-mail')
    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile('org.springframework.security:spring-security-test')

    runtime('com.h2database:h2')
    compileOnly('org.projectlombok:lombok')
    compile group: 'org.jsoup', name: 'jsoup', version: '1.11.3'
    compile group: 'commons-io', name: 'commons-io', version: '2.6'
    compile group: 'joda-time', name: 'joda-time', version: '2.10'
    compile group: 'org.glassfish.jaxb', name: 'jaxb-runtime', version: '2.4.0-b180830.0438'
}

node {
    version = '8.11.3'
    npmVersion = '6.2.0'
    download = true
    workDir = file("${project.projectDir}/frontend/node")
    npmWorkDir = file("${project.projectDir}/frontend/npm")
}

task frontendInstall(type: NpmTask) {
    workingDir = file("${project.projectDir}/frontend")
    args = ['install']
}

task frontendBuild(type: NpmTask, dependsOn: frontendInstall) {
    workingDir = file("${project.projectDir}/frontend")
    args = ['run', 'buildAll']
}

processResources {
    dependsOn frontendBuild
}